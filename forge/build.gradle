plugins {
    id 'com.github.johnrengelman.shadow'
    id 'dev.architectury.loom'
}

group = 'org.popcraft'
def minecraftVersion = "1.16.5"

configurations {
    implementation.extendsFrom shade
}

dependencies {
    shade(project(":common"))
    minecraft "com.mojang:minecraft:$minecraftVersion"

    //mappings minecraft.officialMojangMappings()
    //mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    def mcpChannel = "snapshot"
    def mcpVer = "20210309-1.16.5"
    mappings "de.oceanlabs.mcp:mcp_$mcpChannel:$mcpVer"

    forge "net.minecraftforge:forge:$minecraftVersion-36.1.24"
}

java {
    withSourcesJar()
}

tasks {
    remapJar {
        input.set(shadowJar.archiveFile)
        archiveClassifier.set(null)
        archiveFileName.set("Chunky-forge-${project.version}.jar")
    }
    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shade]
    }
    jar {
        archiveClassifier.set("dev")
    }
    build {
        dependsOn(remapJar)
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chunky",
                "Specification-Vendor"    : "pop4959",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "pop4959",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
