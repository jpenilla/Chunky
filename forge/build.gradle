plugins {
    id 'com.github.johnrengelman.shadow'
    id 'dev.architectury.loom'
}

group = 'org.popcraft'
def minecraftVersion = "1.16.5"

configurations {
    implementation.extendsFrom shade
}

loom {
    silentMojangMappingsLicense()
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraftVersion"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:$minecraftVersion-36.1.24"

    shade(project(":common"))
}

java {
    withSourcesJar()
}

tasks {
    remapJar {
        input.set(shadowJar.archiveFile)
        archiveClassifier.set(null)
        archiveFileName.set("Chunky-Forge-${project.version}.jar")
    }
    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shade]
    }
    jar {
        archiveClassifier.set("dev")
    }
    build {
        dependsOn(remapJar)
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chunky",
                "Specification-Vendor"    : "pop4959",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "pop4959",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
