buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow'
}

apply plugin: 'net.minecraftforge.gradle'

group = 'org.popcraft'
archivesBaseName = 'chunky'

minecraft {
    mappings channel: 'snapshot', version: '20210309-1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    compile.extendsFrom include
}

dependencies {
    include(project(":common"))
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.13'
}

build.dependsOn shadowJar {
    archiveFileName = "Chunky-${project.version}.jar"
    configurations = [project.configurations.include]
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chunky",
                "Specification-Vendor"    : "pop4959",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "pop4959",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

afterEvaluate {
    reobf {
        shadowJar {
            mappings = createMcpToSrg.output
        }
    }
}
